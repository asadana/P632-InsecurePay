apply plugin: 'com.android.application'
apply from: 'jacoco.gradle'

android {
    signingConfigs {
        config {
            keyAlias 'InsecurePayKey'
            keyPassword 'insecurepay'
            storeFile file("insecurepay-keystore/insecurepay-keystore.jks")
            storePassword 'insecurepay'
        }
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.cigital.insecurepay"
        minSdkVersion 17
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // Enabling multidex support.
        multiDexEnabled true
    }
    lintOptions {
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, absolute paths are displayed for all warnings/errors
        absolutePaths false
        // if true, only report errors
        ignoreWarnings false

        xmlReport false
        htmlReport true
        htmlOutput file("$project.projectDir/../reports/lint/lint-result-" + getDate() + ".html")
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            // For running coverage reports on debug builds
            testCoverageEnabled = true
        }
        pro {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    project.ext { appName = 'InsecurePay' }
                    def newName = output.outputFile.name
                    newName = newName.replace("app-", "$project.ext.appName-")
                    newName = newName.replace(".apk", "-" + defaultConfig.versionName +
                            "-" + defaultConfig.versionCode + ".apk")
                    output.outputFile = new File(output.outputFile.parent, newName)
                }
            }
        }
        free {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.config
        }
    }
    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
}
/*

def customName(variant, defaultConfig) {
    def file = variant.outputFile
    def fileName = file.name.replace(".apk", "-" + defaultConfig.versionName +
            "-" + defaultConfig.versionCode + ".apk")
    fileName = fileName.replace("app-", "InsecurePay-")
    variant.outputFile = new File(file.parent, fileName)
}
*/


def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'
    compile 'com.google.android.gms:play-services-appindexing:8.4.0'
    compile 'com.android.support:support-v4:23.0.1'
    compile 'joda-time:joda-time:2.9.2'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    androidTestCompile 'com.android.support.test:rules:0.4'
    androidTestCompile 'com.android.support.test:runner:0.4'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-web:2.2.1'
}
